buildscript {
    ext.kotlin_version = '1.2.71'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

project.ext {
    aspectjVersion = '1.9.1'
}

dependencies {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url 'http://repository.jetbrains.com/utils' }
        maven { url "https://dl.bintray.com/archinamon/maven" }
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly gradleApi()
    compileOnly 'com.android.tools.build:gradle:3.2.0'
    implementation "org.aspectj:aspectjrt:$aspectjVersion"
    implementation "org.aspectj:aspectjtools:$aspectjVersion"

    testImplementation 'junit:junit:4.10'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation gradleApi()
    testImplementation 'com.android.tools.build:gradle:3.2.0'
}

group = 'com.archinamon'
version = '3.3.0'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

if (project.hasProperty('user') && project.hasProperty('apiKey')) {
    bintray {
        user = project.properties.user
        key = project.properties.apiKey

        configurations = ['archives']
        pkg {
            repo = "maven"
            name = "android-gradle-aspectj"
//            websiteUrl = siteUrl
            vcsUrl = "https://github.com/Archinamon/GradleAspectJ-Android"
            licenses = ["Apache-2.0"]
            publish = true
            version {
                name = project.version
                released = new Date()
            }
        }
    }
}